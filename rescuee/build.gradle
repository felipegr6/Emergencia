apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'jacoco'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

def AndroidSupportVersion = '25.1.0'
def GoogleVersion = '10.0.1'

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
  reports {
    xml.enabled = true
    html.enabled = true
  }

  jacocoClasspath = configurations['androidJacocoAnt']

  def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
  def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
  def mainSrc = "${project.projectDir}/src/main/java"

  sourceDirectories = files([mainSrc])
  classDirectories = files([debugTree])
  executionData = files(["${buildDir}/jacoco/testDebugUnitTest.exec",
                         "${buildDir}/outputs/code-coverage/connected/coverage.ec"])
}

jacoco {}

buildscript {
  repositories {
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.21.5'
  }
}

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"
  defaultConfig {
    applicationId "br.com.fgr.emergencia"
    minSdkVersion 16
    targetSdkVersion 25
    versionCode 6
    multiDexEnabled true
    versionName "1.2"
    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  lintOptions {
    abortOnError false
  }
  signingConfigs {
    config {
      keyAlias 'Emergencia'
      keyPassword 'ab34ef78'
      storeFile file('emergencia.jks')
      storePassword 'ab34ef78'
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
      buildConfigField("boolean", "CRASH_REPORT", "true")
    }

    debug {
      testCoverageEnabled true
      signingConfig signingConfigs.config
      minifyEnabled false
      versionNameSuffix '-DEBUG'
      ext.betaDistributionEmailsFilePath = "beta_distribution_emails.txt"
      buildConfigField("boolean", "CRASH_REPORT", "false")
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  productFlavors {}
}

dependencies {
  compile fileTree(include: 'Parse-*.jar', dir: 'libs')

  compile 'com.android.support:multidex:1.0.1'

  compile "com.android.support:appcompat-v7:$AndroidSupportVersion"
  compile "com.android.support:cardview-v7:$AndroidSupportVersion"
  compile "com.android.support:recyclerview-v7:$AndroidSupportVersion"
  compile "com.android.support:design:$AndroidSupportVersion"

  compile "com.google.android.gms:play-services-maps:$GoogleVersion"
  compile "com.google.android.gms:play-services-location:$GoogleVersion"
  compile "com.google.android.gms:play-services-ads:$GoogleVersion"

  compile "com.google.firebase:firebase-core:$GoogleVersion"
  compile "com.google.firebase:firebase-database:$GoogleVersion"
  compile "com.google.firebase:firebase-messaging:$GoogleVersion"

  compile 'com.jakewharton:butterknife:8.4.0'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

  compile 'com.google.code.gson:gson:2.8.0'
  compile 'de.greenrobot:eventbus:2.4.0'
  compile 'com.parse.bolts:bolts-android:1.4.0'

  compile 'com.squareup.retrofit2:retrofit:2.1.0'
  compile 'com.squareup.retrofit2:converter-gson:2.1.0'
  compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
  compile 'com.squareup.okhttp3:okhttp:3.5.0'
  compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
  compile 'com.squareup.okio:okio:1.11.0'

  compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
    transitive = true;
  }
  compile('com.mopub:mopub-sdk:4.11.0@aar') {
    transitive = true;
  }
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-library:1.3'
  testCompile 'org.mockito:mockito-core:2.1.0'
  androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.6.3'
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
  androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.2') {
    exclude module: 'mockito-core'
  }
  // https://mvnrepository.com/artifact/com.parse/parse-android
  compile group: 'com.parse', name: 'parse-android', version: '1.13.1'
}

apply plugin: 'com.google.gms.google-services'
